import java.security.SecureRandom

buildscript {
    ext.kotlin_version = '1.3.40'
    ext.ktor_version = '1.1.5'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://jcenter.bintray.com" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        // classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
mainClassName = 'saarschleife.ServerMain'
apply plugin: 'com.github.johnrengelman.shadow'
// apply plugin: "com.moowork.node"


shadowJar {
    baseName = 'Saarschleife-Server'
    classifier = null
    version = null
}


// processResources.dependsOn 'npmInstall'


sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn(':saarschleife-frontend:compileKotlin2Js')
    dependsOn('generateSecretKey')
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task generateSecretKey() {
    doLast {
        def bytes = new byte[32]
        new SecureRandom().nextBytes(bytes)
        def key = ""
        for (byte b: bytes)
            key += String.format("%02x", b)
        file("$projectDir/src/main/kotlin/saarschleife/key.kt").text = """package saarschleife

fun String.hexStringToByteArray() = ByteArray(this.length / 2) { this.substring(it * 2, it * 2 + 2).toInt(16).toByte() }

// Auto-generated at build time. Rebuild project to change the key.
val secretKey = "$key".hexStringToByteArray()
"""
    }
}

// Additional resources (node_modules and frontend project)
processResources.dependsOn ':saarschleife-frontend:compileKotlin2Js'
sourceSets.main.resources.srcDirs += ["node_modules/materialize-css/dist"]
sourceSets.main.resources.srcDirs += ["../frontend/web"]


repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile 'org.litote.kmongo:kmongo:3.10.1'
    compile 'org.litote.kmongo:kmongo-async:3.10.1'
    compile 'org.litote.kmongo:kmongo-coroutine:3.10.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}